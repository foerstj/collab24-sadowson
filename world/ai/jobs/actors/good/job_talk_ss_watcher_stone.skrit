/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_ss_watcher_stone.skrit

	Author(s)	: 	Sadowson

	Purpose		: 	Handles progressing the secret quest.

	(C)opyright 2024 Sadowson.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go			m_Go$;
GoMind		m_Mind$;
GoBody  	m_Body$;
Job			m_Job$;
Goid		m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}


event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Face$;
}

////////////////////////////////////////////////////////////////////////////////
//	face the audience
state Face$
{
	transition
	{
		-> Talk$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
	}

	event OnEnterState$
	{
		if( !m_Go$.HasConversation )
		{
			report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
							m_Go$.GetTemplateName,
							m_Go$.Goid );
			SetState Exiting$;
		}
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );
	}
}


////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}
	
	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('talk');
	}

	event OnEnterState$
	{

		string	conversation_block$;
		int		Secret_Quest_State$;
		
		conversation_block$ = "conversation_watcher_stone";
		
		if( GameAuditor.GetDb.GetBool( "secret_quest_active" ) )
		{
			if( GameAuditor.GetDb.GetBool( "secret_quest_watcher_stone_complete" ) != true )
			{
				Secret_Quest_State$ = GameAuditor.GetDB.GetInt( "secret_quest_state" );
				if( Secret_Quest_State$ == 5 )
				{
					GameAuditor.GetDb.SetInt( "secret_quest_state", 6 );
					GameAuditor.GetDb.SetBool( "secret_quest_watcher_stone_complete", true );
					Report.Generic( "Secret Quest State: 6" );
				}
			}
		}
		
		//Use Conversation
		m_go$.GetConversation.RSSelectConversation( conversation_block$ );
		TryAnim$('talk');
		
		//Start talking
		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		// $$$ close up any dialogs here...

		// Send a PL_FACE reguest without a target so that they no longer
		// continue to face the character they were talking to
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE );

		m_Job$.MarkForDeletion();
	}
}


