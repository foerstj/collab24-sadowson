/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_ss_archivist_library.skrit

	Author(s)	: 	Sadowson

	Purpose		: 	Generic script

	(C)opyright 2024 Sadowson.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go			m_Go$;
GoMind		m_Mind$;
GoBody  	m_Body$;
Job			m_Job$;
Goid		m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}


event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Face$;
}

////////////////////////////////////////////////////////////////////////////////
//	face the audience
state Face$
{
	transition
	{
		-> Talk$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
	}

	event OnEnterState$
	{
		if( !m_Go$.HasConversation )
		{
			report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
							m_Go$.GetTemplateName,
							m_Go$.Goid );
			SetState Exiting$;
		}
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );
	}
}


////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}
	
	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('talk');
	}

	event OnEnterState$
	{
		/*
		conversation_archivist_library_hello			-> why_not
		conversation_archivist_library_whynot			-> what_about_you
		conversation_archivist_library_whataboutyou

		*/
		
		string conversation_block$;
		conversation_block$ = "conversation_archivist_library_hello";
		
		if( GameAuditor.GetDb.GetBool( "why_not" ) )
		{
			conversation_block$ = "conversation_archivist_library_whynot";
			GameAuditor.GetDb.SetBool( "archivist_name", true );
		}
		
		if( GameAuditor.GetDb.GetBool( "what_about_you" ) )
		{
			conversation_block$ = "conversation_archivist_library_whataboutyou";
			if( GameAuditor.GetDb.GetInt( "secret_quest_state" ) == 4 )
			{
				GameAuditor.GetDb.SetInt( "secret_quest_state", 5 );
				Report.Generic( "Secret Quest state: 5" );
			}
		}

		m_go$.GetConversation.RSSelectConversation( conversation_block$ );
		TryAnim$('talk');
		m_Go$.GetConversation.RSActivateDialogue();//Start talking
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		// $$$ close up any dialogs here...

		// Send a PL_FACE reguest without a target so that they no longer
		// continue to face the character they were talking to
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE );

		m_Job$.MarkForDeletion();
	}
}


