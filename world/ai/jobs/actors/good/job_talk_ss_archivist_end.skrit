/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_ss_archivist_death_valley.skrit

	Author(s)	: 	Sadowson

	Purpose		: 	Generic script which checks the actors highest skill level and then activates the relevant conversation.

	(C)opyright 2024 Sadowson.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go			m_Go$;
GoMind		m_Mind$;
GoBody  	m_Body$;
Job			m_Job$;
Goid		m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}


event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Face$;
}

////////////////////////////////////////////////////////////////////////////////
//	face the audience
state Face$
{
	transition
	{
		-> Talk$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
	}

	event OnEnterState$
	{
		if( !m_Go$.HasConversation )
		{
			report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
							m_Go$.GetTemplateName,
							m_Go$.Goid );
			SetState Exiting$;
		}
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );
	}
}


////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}
	
	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('talk');
	}

	event OnEnterState$
	{
		/*
		conversation_archivist_end			-> For everyone who skipped exposition
		conversation_archivist_end_alt		-> one_empty | requires archivist_name
		conversation_archivist_end_secret	-> who_is_she
		conversation_archivist_end_whoisshe
		*/
		
		string conversation_block$;
		conversation_block$ = "conversation_archivist_end";
		
		if( GameAuditor.GetDb.GetInt( "secret_quest_state" ) == 6 )
		{
			conversation_block$ = "conversation_archivist_end_alt";
		}
		
		if( GameAuditor.GetDb.GetBool( "one_empty" ) )
		{
			conversation_block$ = "conversation_archivist_end_secret";
		}
		
		if( GameAuditor.GetDb.GetBool( "who_is_she" ) )
		{
			conversation_block$ = "conversation_archivist_end_whoisshe";
			if( GameAuditor.GetDb.GetInt( "secret_quest_state" ) == 6 )
			{
				GameAuditor.GetDb.SetBool( "Athena_secret_known", true );
				GameAuditor.GetDb.SetInt( "secret_quest_state", 7 );
				Report.Generic( "Athena_secret_known: TRUE" );
				Report.Generic( "secret_quest_State: 7" );
			}
			GameAuditor.GetDb.SetBool( "who_is_she", false );
		}
			m_go$.GetConversation.RSSelectConversation( conversation_block$ );
			TryAnim$('talk');
			m_Go$.GetConversation.RSActivateDialogue();//Start talking
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		// $$$ close up any dialogs here...

		// Send a PL_FACE reguest without a target so that they no longer
		// continue to face the character they were talking to
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE );

		m_Job$.MarkForDeletion();
	}
}


